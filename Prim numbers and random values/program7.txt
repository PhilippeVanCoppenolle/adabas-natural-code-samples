*
* PRIME NUMBERS USING SIEVE OF ERATOSTHENES
* 
* A prime number is a number that has only two factors: 1 and itself. 
* By convention, 0 and 1 are not primes. 
* For example, 2, 3, 5, 7, 11 are prime numbers. 
*
DEFINE DATA LOCAL
01 #LIMIT (I4)
01 #INDEX (I4)
01 #J (I4)
01 #SIEVE-ARRAY (L/0:*)
01 #MULTIPLE  (I4)
01 #LOOP-LIMIT (I4)
01 #PRIME-LIST (A79)
01 #PRIME-SUM (I4)
01 #CPU-START (P9)
01 #CPU-ELAPSED (P9.3)
END-DEFINE
*     
OPTIONS MCG=(INDX,OVFLW,ZD=OFF)
MOVE 1000000 TO #LIMIT
INPUT 'Calculate primes from to:' #LIMIT (AD=MIL)
WRITE 'Calculate primes from 1 to' #LIMIT (EM=ZZ,ZZZ,ZZ9)
*
ST1.  SETTIME
MOVE *CPU-TIME TO #CPU-START
EXPAND ARRAY #SIEVE-ARRAY TO (0:#LIMIT)
*
MOVE TRUE TO #SIEVE-ARRAY (*)
MOVE FALSE TO #SIEVE-ARRAY (0) #SIEVE-ARRAY (1)
*
COMPUTE #LOOP-LIMIT = SQRT(#LIMIT) + 1
FOR #INDEX 2 TO #LOOP-LIMIT
  FOR #J 2 #LIMIT
    COMPUTE #MULTIPLE = #J * #INDEX
    IF #MULTIPLE > #LIMIT
      ESCAPE BOTTOM
    END-IF
    MOVE FALSE TO #SIEVE-ARRAY (#MULTIPLE)
  END-FOR
*
END-FOR
*
WRITE 'LOCAL ELAPSED TIME:' *TIMD (ST1.) (EM=99:99:99'.'9) '(HH:MM:SS.T)'
COMPUTE #CPU-ELAPSED = (*CPU-TIME - #CPU-START) / 100
WRITE '=' #CPU-ELAPSED '(seconds)'
*
FOR #INDEX 1 TO 10000
  IF #SIEVE-ARRAY (#INDEX) 
    COMPUTE #PRIME-SUM = #PRIME-SUM + #INDEX
  END-IF 
END-FOR
WRITE 'Verification Test'
WRITE 'Computed sum of primes of 1 to 10000:' #PRIME-SUM
WRITE 'The known answer is 5736396'
END
